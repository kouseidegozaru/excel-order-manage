VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "Sheet1"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
'行削除を検知するための変更前の行数
Private lastUpdatedRowCnt As Long

Private Sub Worksheet_Change(ByVal target As Range)

    'セルの変更をフック
    HookCellChange target
    
End Sub

Private Sub HookCellChange(ByVal target As Range)
    'IsIgnoreChangeEventsはシートの変更を検知するかどうかの状態を設定
    'events.Ignoreはシートの変更があっても無視する

    'データアクセス
    Dim dataStorage As New DataBaseAccesser

    Dim order As New OrderSheetAccesser

    ' セルの変更を監視
    Dim events As New HookChange
    With events
    
        '変更された範囲をセット
        .SetRange target
        '監視の有無をセット
        .Ignore IsIgnoreChangeEvents
        
        
        
'行の削除を監視
        If .IsDeleted(lastUpdatedRowCnt, order.DataNextRowIndex) Then

        End If
        
          
          
' 担当者コードの変更を監視
        If .IsChanged(order.UserCodeRange) Then
            IsIgnoreChangeEvents = True
             
            ' 担当者名の表示
            order.UserName = dataStorage.GetUserName(order.userCode)
            ' 発注データの読み込み
            LoadData
            
            IsIgnoreChangeEvents = False
        End If
        
      
      
'部門コードの変更を監視
        If .IsChanged(order.BumonCodeRange) Then
            IsIgnoreChangeEvents = True
            
            ' 部門名の表示
            order.BumonName = dataStorage.GetBumonName(order.bumonCode)
            ' 発注データの読み込み
            LoadData
            
            IsIgnoreChangeEvents = False
        End If
        
        
      
'商品コードの変更を監視
        If .IsChanged(order.ProductsCodeRange) Then
            IsIgnoreChangeEvents = True
            
            ' 商品情報の表示
            DisplayProductsInfo target
            
            IsIgnoreChangeEvents = False
        End If
        
        
    
'数量の変更を監視
        If .IsChanged(order.QtyRange) Then

        End If
        
        

'日付の変更を監視
        If .IsChanged(order.TargetDateRange) Then
            IsIgnoreChangeEvents = True
            
            ' 発注データの読み込み
            LoadData
            
            IsIgnoreChangeEvents = False
        End If
      
              
    End With
    
    '最終行数を更新
    lastUpdatedRowCnt = order.DataNextRowIndex
End Sub
